/**
 * File:	modules/PamSettings.ycp
 * Package:	yast2-pam
 * Summary:	YaST intrerface for /etc/default/passwd agent
 * Authors:	Jiri Suchomel <jsuchome@suse.cz>
 * Flags:	Unstable
 *
 * $Id$
 *
 */

{

module "PamSettings";

import "Pam";

/**
 * Was /etc/default/passwd modified?
 */
boolean default_passwd_modified	= false;

list<string> security_files = [];

//---------------------------- functions for handling /etc/security/*.conf

/**
 * Returns list of files currently handled by the module
 * @return list
 */
global define list<string> GetFiles () ``{

    y2warning ("this function is obsolete, use Pam module instead");
    if (security_files == [])
	security_files = (list<string>) SCR::Dir (.etc.security.section);
    if (security_files == nil)
	security_files = [];
    return security_files;
}

/**
 * Form absolute path name.
 * @param file name (or unambiguous substring, like "unix2" or "pwcheck")
 * @return path to file
 */
define string Filename (string name) ``{

    y2warning ("this function is obsolete, use Pam module instead");
    return "";
}

/**
 * Returns the values (options) of one type in the given file
 * @param file file name
 * @param type type of management group (these are used for authentication,
 *  account management and password management)
 * @return list of options
 * @example
 * GetValues ("pwcheck", "password") returns [ "nullok", "cracklib" ]
 */
global define list<string> GetValues (string file, string type) ``{

    y2warning ("this function is obsolete, use Pam module instead");
    // FIXME temporary hack for backward compatibility
    if (file == "pam_pwcheck" && type == "password")
    {
	map pwcheck = Pam::Query ("pwcheck");
	return pwcheck["password"]:[];
    }
    return [];
}

/**
 * Returns the given file contents in the map form
 * @param file file name
 * @return map which maps type to list of its options
 * @example
 * GetFile ("pwcheck") returns $[ "password" : [ "nullok", "cracklib" ] ]
 */
global define map GetFile (string file) ``{

    y2warning ("this function is obsolete, use Pam module instead");
    return $[];
}


/**
 * Adds a new options to the current list of options
 * @param file file name
 * @param type type of management group
 * @return success
 * @example
 * AddValue ("pwcheck", "password", "md5" ])
 */
global define boolean AddValue (string file, string type, string value) ``{
    y2warning ("this function is obsolete, use Pam module instead");
    if (file == "pam_pwcheck")
    {
	if (value == "no_obscure_checks")
	    return Pam::Add ("pwcheck-no_obscure_checks");
	if (value == "cracklib")
	    return Pam::Add ("pwcheck-cracklib");
	list split = splitstring (value, "=");
	string pth = split[1]:"";
	if (issubstring (value, "cracklib="))
	    return Pam::Add ("pwcheck-cracklib-path=" + pth);
	if (issubstring (value, "minlen="))
	    return Pam::Add ("pwcheck-minlen=" + pth);
	if (issubstring (value, "remember="))
	    return Pam::Add ("pwcheck-remember=" + pth);
    }
    else if (file == "unix2" || file == "pam_unix2")
    {
	if (value == "use_ldap")
	    return Pam::Add ("ldap");
	if (value == "use_krb5")
	    return Pam::Add ("krb5");
	if (value == "winbind")
	    return Pam::Add ("winbind");
    }
    return false;
}

/**
 * Deletes an options from the current list of options
 * @param file file name
 * @param type type of management group
 * @return success
 * @example
 * RemoveValue ("pwcheck", "password", "md5" ])
 */
global define boolean RemoveValue (string file, string type, string value) ``{
    y2warning ("this function is obsolete, use Pam module instead");
    if (file == "pam_pwcheck")
    {
	if (value == "no_obscure_checks")
	    return Pam::Remove ("pwcheck-no_obscure_checks");
	if (issubstring (value, "cracklib"))
	    return Pam::Remove ("pwcheck-cracklib");
	if (issubstring (value, "minlen="))
	    return Pam::Remove ("pwcheck-minlen=");
	if (issubstring (value, "remember="))
	    return Pam::Remove ("pwcheck-remember=");
    }
    else if (file == "unix2" || file == "pam_unix2")
    {
	if (value == "use_ldap")
	    return Pam::Remove ("ldap");
	if (value == "use_krb5")
	    return Pam::Remove ("krb5");
	if (value == "winbind")
	    return Pam::Remove ("winbind");
    }
    return false;
}

/**
 * Saves the new list of options of one type to given file
 * @param file file name
 * @param type type of management group
 * @return success
 * @example
 * SetValues ("pwcheck", "password", [ "cracklib", "md5" ])
 */
global define boolean SetValues (string file, string type, list<string> values) ``{
    y2warning ("this function is obsolete, use Pam module instead");
    return false;
}

//---------------------------- functions for handling /etc/default/passwd

/**
 * Reads the value of default crypt hash (defined in /etc/default/passwd)
 */
global define string GetHashMethod () {
    string m = (string) SCR::Read (.etc.default.passwd.crypt);
    if (m == nil || m == "")
	m	= "md5";
    return m;
}

/**
 * Reads the value of default crypt hash for group passwords
 */
global define string GetGroupHashMethod () {

    any g = SCR::Read (.etc.default.passwd.group_crypt);
    if (g != nil && is (g, string) && (string) g != "")
	return (string) g;
    else return GetHashMethod ();
}

/**
 * Reads the value from /etc/default/passwd
 */
global define string GetDefaultValue (string key) {
    return (string) SCR::Read (add (.etc.default.passwd, key));
}

/**
 * Sets the new value of default crypt hash - modifies /etc/default/passwd !
 * @param hash the new value of hash
 * @example
 * SetValues ("md5")
 */
global define boolean SetHashMethod (string hash) {
    if (GetHashMethod () != hash)
    {
	default_passwd_modified	= true;
    }
    default_passwd_modified	= true;
    return SCR::Write (.etc.default.passwd.crypt, hash);
}

/**
 * Sets the new value of default crypt hash for group passwords
 * @param hash the new value of hash
 */
global define boolean SetGroupHashMethod (string hash) {
    if (GetGroupHashMethod () != hash)
    {
	default_passwd_modified	= true;
    }
    return SCR::Write (.etc.default.passwd.group_crypt, hash);
}

/**
 * Set the value of key in /etc/default/passwd
 */
global define boolean SetDefaultValue (string key, string value) {
    if (GetDefaultValue (key) != value)
    {
	default_passwd_modified	= true;
    }
    return SCR::Write (add (.etc.default.passwd, key), value);
}



//---------------------------- common functions ------------------------------

/**
 * Writes all edited files to the disk
 * @param force - write everythink, even if modification was not detected
 * @return true on success
 */
global define boolean Write (boolean force) {

    boolean ret = true;
    if (default_passwd_modified || force)
	ret	= ret && SCR::Write (.etc.default.passwd, nil);
    return ret;
}

}//EOF
