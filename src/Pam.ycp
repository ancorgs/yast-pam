/* ------------------------------------------------------------------------------
 * Copyright (c) 2006-2012 Novell, Inc. All Rights Reserved.
 *
 *
 * This program is free software; you can redistribute it and/or modify it under
 * the terms of version 2 of the GNU General Public License as published by the
 * Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with
 * this program; if not, contact Novell, Inc.
 *
 * To contact Novell about this file by physical or electronic mail, you may find
 * current contact information at www.novell.com.
 * ------------------------------------------------------------------------------
 */

/**
 * File:	modules/Pam.ycp
 * Package:	yast2-pam
 * Summary:	YaST intrerface for /etc/pam.d/* files
 * Authors:	Jiri Suchomel <jsuchome@suse.cz>
 * Flags:	Unstable
 *
 * $Id$
 *
 */

{

module "Pam";

/**
 * Query PAM configuration for status of given module
 * @param string PAM module (e.g. ldap, cracklib)
 * @return map <string,list> keys are 'management groups' (e.g. auth), values
 * are lists of options
 */
global map Query (string mod) {

    map ret = $[];
    map out = (map)SCR::Execute (.target.bash_output, "/usr/sbin/pam-config -q --" + mod);
    if (out["exit"]:0 != 0)
    {
	y2warning ("pam-config for %1 returned %2", mod, out);
	return ret;
    }
    foreach (string line, splitstring (out["stdout"]:"", "\n"), {
	list l = splitstring (line, ":");
	if (line == "" || size (l) < 2) return;
	string key	= l[0]:"";
	ret[key]	=
	     filter (string o, splitstring (l[1]:"", " \t"), ``(o != ""));
    });
    return ret;
}

/**
 * Ask if given PAM module is enabled
 */
global boolean Enabled (string mod) {

    return size (Query(mod)) > 0;
}

/**
 * Add options or new PAM module
 * @param string PAM module or option
 * @return success
 */
global boolean Add (string mod) {

    map out = (map)SCR::Execute (.target.bash_output, "/usr/sbin/pam-config -a --" + mod);
    if (out["exit"]:0 != 0)
    {
	y2warning ("pam-config for %1 returned %2", mod, out);
	return false;
    }
    return true;
}

/**
 * Remove options or PAM module
 * @param string PAM module or option
 * @return success
 */
global boolean Remove (string mod) {

    map out = (map)SCR::Execute (.target.bash_output, "/usr/sbin/pam-config -d --" + mod);
    if (out["exit"]:0 != 0)
    {
	y2warning ("pam-config for %1 returned %2", mod, out);
	return false;
    }
    return true;
}

/**
 * Add/Remove option or PAM module
 * @param string PAM module or option
 * @param boolean true for adding, false for removing
 * @return success
 */
global boolean Set (string mod, boolean set) {
    return set ? Add (mod) : Remove (mod);
}

}//EOF
